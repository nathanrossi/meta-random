MACHINEOVERRIDES = "raspberrypi:rpi:${MACHINE}"

DEFAULTTUNE ?= "arm1176jzfshf"
require conf/machine/include/tune-arm1176jzf-s.inc

MACHINE_EXTRA_RRECOMMENDS += " \
        linux-firmware-bcm43430 \
        kernel-module-bcm2835-v4l2 \
        kernel-module-brcmfmac \
        kernel-module-brcmutil \
        kernel-module-lan78xx \
        kernel-module-vc4 \
        kernel-module-vchiq \
        kernel-modules \
        userland \
        "

KERNEL_IMAGETYPE = "zImage"
KERNEL_DEVICETREE = " \
        bcm2835-rpi-b.dtb \
        bcm2835-rpi-b-plus.dtb \
        bcm2835-rpi-b-rev2.dtb \
        bcm2835-rpi-zero.dtb \
        bcm2835-rpi-zero-w.dtb \
        "

# Raspberry Pi has no hardware clock
MACHINE_FEATURES_BACKFILL_CONSIDERED = "rtc"
MACHINE_FEATURES += "apm usbhost keyboard vfat ext2 screen touchscreen alsa bluetooth wifi sdio vc4graphics"

IMAGE_BOOT_FILES ?= " \
        bcm2835-rpi-b-rev2.dtb \
        ${KERNEL_IMAGETYPE};kernel.img \
        bcm2835-bootfiles/*.txt \
        bcm2835-bootfiles/*.bin \
        bcm2835-bootfiles/*.dat \
        bcm2835-bootfiles/*.elf \
        "

# ensure bootfiles are available for wic image building
do_image_wic[depends] += "bcm2835-bootfiles:do_deploy"
# ensure the image is built with cpio.gz
IMAGE_FSTYPES_append_pn-core-image-minimal = " cpio cpio.gz"
IMAGE_BOOT_FILES_append_pn-disk-image-minimal = " \
        core-image-minimal-${MACHINE}.cpio.gz;initramfs.gz \
        "

# needs at least 128M for h264 camera
GPU_MEM = "128"
VIDEO_CAMERA = "1"

# something broke followkernel, so point it at 0x08000000 aka @128M
RPI_EXTRA_CONFIG = '\ndevice_tree=bcm2835-rpi-b-rev2.dtb\ninitramfs initramfs.gz 0x08000000\n'

